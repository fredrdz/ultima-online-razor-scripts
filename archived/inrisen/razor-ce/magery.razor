#SCRIPT: Magery
#Author: Talik Starr
#Shard: IN:RISEN

clearsysmsg
createlist 'regs'
clearlist 'regs'
#Bloodmoss
pushlist 'regs' '3963'
#Ash
pushlist 'regs' '3980'
#Mandrake
pushlist 'regs' '3974'
#Ginseng
pushlist 'regs' '3973'
#Spiders Silk
pushlist 'regs' '3981'
#Black Pearl
pushlist 'regs' '3962'
#Nightshade
pushlist 'regs' '3976'
#Garlic
pushlist 'regs' '3972'

while not dead
    if skill 'magery' >= 100
        overhead 'GM MAGERY, DONE.'
        overhead 'Stopping script'
        stop
    endif
    
    # reagents count check
    createtimer 'regctx'
    foreach 'reg' in 'regs'
        if not findtype 'reg' backpack
            @setvar! 'restocking' 1
            break
        endif
        if timer 'regctx' > 5000
            break
        endif
    endfor
    
    if 'restocking' == 1
        overhead '[RESTOCKING]' 53
        restock 1
        wft 3000
        target 'self'
        for 15
            if insysmsg 'Restock done,'
                break
            endif
            wait 1000
        endfor
        @setvar! 'restocking' 0
    endif

    # bandage count check
    if count 'clean bandage%s%' == 0
        overhead "No more bandaids!" 34
        stop
    endif
    
    # wait check
    if insysmsg 'You must wait' or insysmsg 'World save'
        overhead '[WAITING]' 53
        wait 1500
    endif

    # checks HP so we dont kill ourselves and then casts spell
    if skill 'magery' >= 30.1 and skill 'magery' <= 60
        @setvar! minMana 10
        if hits >= 30 and mana >= minMana
            cast 'lightning'
            waitfortarget 3000
            target 'self'
            wait 3100
        endif
    elseif skill 'magery' >= 60.1 and skill 'magery' <= 85
        @setvar! minMana 20
        if hits >= 60 and mana >= minMana
            cast 'energy bolt'
            waitfortarget 3000
            target 'self'
            wait 3100
         endif
    elseif skill 'magery' >= 85.1 and skill 'magery' < 100
        @setvar! minMana 40
        if hp >= 90 and mana >= minMana
            cast 'flamestrike'
            waitfortarget 3000
            target 'self'
            wait 3100
        endif
    endif
    
    if poisoned
        cast 'cure'
        wft 1000
        target 'self'
        wait 100
        for 10
            if insysmsg 'You have been cured'
                overhead 'CURED!'
                break
            endif
            wait 100
         endfor
    endif
    
    if insysmsg 'You cannot move' or insysmsg 'You are frozen'
        overhead 'PARALYZED!'
        cast 'magic arrow'
        wft 1000
        target 'self'
        wait 1200
        clearsysmsg
    endif

    if diffhp >= 10 and findtype "clean bandage%s%" backpack as baids
        if timer 'bhealcd' > 2700
            dclick baids
            wft 1000
            target 'self'
            createtimer 'bhealcd'
        endif
    endif
    
    # clean bandages via water trough
    if count 'bloody bandage%s%' > 1 and findtype 'water trough' true
        dclicktype 'bloody bandage%s%'
        wft 500
        targettype 'water trough' true
    endif
    
    # heal/gheal reg check, disables magic healing if out
    if count '3972' == 0 or count '3973' == 0 or count '3981' == 0 or count '3974' == 0
        settimer 'mhealcd' 0
    endif

    # magic healing overboost
    if diffhp > 30 and hp >= 40 and mana > 5
        if timer "mhealcd" > 1000
            cast 'Heal'
            wft 1000
            target 'self'
            settimer 'mhealcd' 0
        endif
    elseif hp < 40 and mana > 10
        if timer 'mhealcd' > 2000
            cast 'Greater Heal'
            wft 1000
            target 'self'
            settimer 'mhealcd' 0
        endif
    endif


    # get some mana
    if mana < minMana and not warmode
        useskill 'Meditation'
        while diffmana > 0
            if diffhp >= 10 or poisoned
                break
            elseif insysmsg 'You cannot move' or insysmsg 'You are frozen'
                break
            elseif insysmsg 'cannot focus'
                break
            elseif insysmsg 'You begin meditating...'
                for 45
                    if diffhp >= 10
                        break
                    elseif insysmsg 'You enter a meditative trance.'
                        overhead '[MEDITATING]'
                        break
                    endif
                    wait 100
                endfor
            elseif insysmsg 'mana for this spell'
                break
            elseif insysmsg 'You are preoccupied' or warmode
                hotkey 'Toggle Peace Only'
                break
            endif
            wait 100
        endwhile
    endif
    # while not dead wait
    wait 100
endwhile

# stop script if dead
if dead
    stop
endif
